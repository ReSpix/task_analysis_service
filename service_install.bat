@echo off
chcp 65001 >nul
setlocal enabledelayedexpansion

REM ==== –ü–†–û–í–ï–†–ö–ê –ü–†–ê–í –ê–î–ú–ò–ù–ò–°–¢–†–ê–¢–û–†–ê ====
fltmc >nul 2>&1
if errorlevel 1 (
    echo.
    echo [–ò–ù–§–û] –¢—Ä–µ–±—É—é—Ç—Å—è –ø—Ä–∞–≤–∞ –∞–¥–º–∏–Ω–∏—Å—Ç—Ä–∞—Ç–æ—Ä–∞. –ü–µ—Ä–µ–∑–∞–ø—É—Å–∫...
    powershell -Command "Start-Process -Verb RunAs -FilePath '%~f0'"
    exit /b
)


REM ==== –ü–ï–†–ï–ô–¢–ò –í –î–ò–†–ï–ö–¢–û–†–ò–Æ –°–ö–†–ò–ü–¢–ê ====
cd /d "%~dp0"


REM ==== –ù–ê–°–¢–†–û–ô–ö–ò ====
call settings.bat
set SERVICE_NAME=AsanaIntegrationService
set SERVICE_DISPLAY_NAME=Asana Integration Service
set SERVICE_DESCRIPTION=–°–µ—Ä–≤–∏—Å –¥–ª—è –∏–Ω—Ç–µ–≥—Ä–∞—Ü–∏–∏ —á–µ–ª–ª–µ–Ω–¥–∂–µ–π: Asana, –Ø–Ω–¥–µ–∫—Å –§–æ—Ä–º—ã, —É–≤–µ–¥–æ–º–ª–µ–Ω–∏—è –¢–µ–ª–µ–≥—Ä–∞–º –∏ –æ—Ç—á–µ—Ç—ã
set VENV_DIR=.venv
set PYTHON_EXE=%~dp0%VENV_DIR%\Scripts\python.exe
set PROJECT_DIR=%~dp0\src
set MAIN_MODULE=main:app
set LOG_DIR=%~dp0data
set NSSM_EXE=%~dp0nssm-2.24\win64\nssm.exe
set REQUIREMENTS_FILE=src\requirements.txt

REM ==== –ü—Ä–æ–≤–µ—Ä–∫–∞ —Å—É—â–µ—Å—Ç–≤–æ–≤–∞–Ω–∏—è —Å–ª—É–∂–±—ã ====
sc query "%SERVICE_NAME%" >nul 2>&1
if %errorlevel%==0 (
    echo ============================================
    echo [–û–®–ò–ë–ö–ê] –°–ª—É–∂–±–∞ "%SERVICE_NAME%" —É–∂–µ —Å—É—â–µ—Å—Ç–≤—É–µ—Ç.
    echo –£–¥–∞–∏—Ç–µ –µ–µ —Å –ø–æ–º–æ—â—å—é:
    echo service_remove.bat
    echo –ü–æ—Å–ª–µ —á–µ–≥–æ —ç—Ç–æ—Ç —Ñ–∞–π–ª –º–æ–∂–Ω–æ –±—É–¥–µ—Ç –∑–∞–ø—É—Å—Ç–∏—Ç—å —Å–Ω–æ–≤–∞
    echo ============================================
    pause
    exit /b 1
)

echo ============================================
echo [1/5] –ü—Ä–æ–≤–µ—Ä–∫–∞ Python
where python >nul 2>nul
if errorlevel 1 (
    echo [–û–®–ò–ë–ö–ê] Python –Ω–µ —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω –∏–ª–∏ –Ω–µ –≤ PATH
    pause
    exit /b 1
)

echo ============================================
echo [2/5] –°–æ–∑–¥–∞–Ω–∏–µ –≤–∏—Ä—Ç—É–∞–ª—å–Ω–æ–≥–æ –æ–∫—Ä—É–∂–µ–Ω–∏—è
if not exist "%VENV_DIR%" (
    python -m venv %VENV_DIR%
    if errorlevel 1 (
        echo [–û–®–ò–ë–ö–ê] –ù–µ —É–¥–∞–ª–æ—Å—å —Å–æ–∑–¥–∞—Ç—å –≤–∏—Ä—Ç—É–∞–ª—å–Ω–æ–µ –æ–∫—Ä—É–∂–µ–Ω–∏–µ
        pause
        exit /b 1
    )
) else (
    echo [–ò–ù–§–û] –í–∏—Ä—Ç—É–∞–ª—å–Ω–æ–µ –æ–∫—Ä—É–∂–µ–Ω–∏–µ —É–∂–µ —Å—É—â–µ—Å—Ç–≤—É–µ—Ç
)

echo ============================================
echo [3/5] –£—Å—Ç–∞–Ω–æ–≤–∫–∞ –∑–∞–≤–∏—Å–∏–º–æ—Å—Ç–µ–π
if exist "%REQUIREMENTS_FILE%" (
    "%PYTHON_EXE%" -m pip install --upgrade pip
    "%PYTHON_EXE%" -m pip install -r "%REQUIREMENTS_FILE%"
    if errorlevel 1 (
        echo [–û–®–ò–ë–ö–ê] –ù–µ —É–¥–∞–ª–æ—Å—å —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å –∑–∞–≤–∏—Å–∏–º–æ—Å—Ç–∏
        pause
        exit /b 1
    )
) else (
    echo [–û–®–ò–ë–ö–ê] –§–∞–π–ª requirements.txt –Ω–µ –Ω–∞–π–¥–µ–Ω
    pause
    exit /b 1
)

echo ============================================
echo [4/5] –£—Å—Ç–∞–Ω–æ–≤–∫–∞ —Å–ª—É–∂–±—ã —á–µ—Ä–µ–∑ NSSM

if not exist "%NSSM_EXE%" (
    echo [–û–®–ò–ë–ö–ê] NSSM –Ω–µ –Ω–∞–π–¥–µ–Ω –ø–æ –ø—É—Ç–∏: %NSSM_EXE%
    pause
    exit /b 1
)

REM –£–¥–∞–ª–µ–Ω–∏–µ —Å—Ç–∞—Ä–æ–π —Å–ª—É–∂–±—ã (–µ—Å–ª–∏ –µ—Å—Ç—å)
"%NSSM_EXE%" remove %SERVICE_NAME% confirm >nul 2>&1

REM –£—Å—Ç–∞–Ω–æ–≤–∫–∞ –Ω–æ–≤–æ–π —Å–ª—É–∂–±—ã
"%NSSM_EXE%" install %SERVICE_NAME% "%PYTHON_EXE%"
if errorlevel 1 (
    echo [–û–®–ò–ë–ö–ê] –ù–µ —É–¥–∞–ª–æ—Å—å —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å —Å–ª—É–∂–±—É
    pause
    exit /b 1
)

"%NSSM_EXE%" set %SERVICE_NAME% AppDirectory "%PROJECT_DIR%"
"%NSSM_EXE%" set %SERVICE_NAME% AppParameters "-m uvicorn %MAIN_MODULE% --host %HOST% --port %PORT%"
"%NSSM_EXE%" set %SERVICE_NAME% DisplayName "%DISPLAY_NAME%"
"%NSSM_EXE%" set %SERVICE_NAME% Description "%DESCRIPTION%"
"%NSSM_EXE%" set %SERVICE_NAME% Start SERVICE_AUTO_START

if not exist "%LOG_DIR%" mkdir "%LOG_DIR%"
"%NSSM_EXE%" set %SERVICE_NAME% AppStdout "%LOG_DIR%\additional_1.log"
"%NSSM_EXE%" set %SERVICE_NAME% AppStderr "%LOG_DIR%\additional_2.log"
"%NSSM_EXE%" set %SERVICE_NAME% AppRestartDelay 5000

echo ============================================
echo [5/5] –ó–∞–ø—É—Å–∫ —Å–ª—É–∂–±—ã
sc start %SERVICE_NAME%

echo ============================================
echo ‚úÖ FastAPI —Å–ª—É–∂–±–∞ —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–∞ –∏ –∑–∞–ø—É—â–µ–Ω–∞
echo üìÇ –õ–æ–≥–∏: %LOG_DIR%

if "%HOST%" == "0.0.0.0" (
    set HOST=127.0.0.1
)

echo üåê URL: http://%HOST%:%PORT%
echo ============================================
pause
